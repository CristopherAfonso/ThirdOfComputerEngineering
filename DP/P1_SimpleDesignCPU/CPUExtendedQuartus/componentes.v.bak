//Componentes varios

//Banco de registros de dos salidas y una entrada
module regfile(input  wire         clk, 
               input  wire         we3,             //se�al de habilitaci�n de escritura
               input  wire [4:0]   ra1, ra2, rasave, wa3,   //direcciones de regs leidos y reg a escribir
               input  wire [15:0]  wd3, 			      //dato a escribir
               output wire [15:0]  rd1, rd2, rdsave);  //datos leidos

  reg [15:0] regb[0:31]; //memoria de 32 registros de 16 bits de ancho

  initial
  begin
    $readmemb("regfile.dat",regb); // inicializa los registros a valores conocidos
  end  
  
  // El registro 0 siempre es cero
  // se leen dos reg combinacionalmente
  // y la escritura del tercero ocurre en flanco de subida del reloj
  always @(posedge clk)
    if (we3) regb[wa3] <= wd3;	
  
  assign rd1 = (ra1 != 0) ? regb[ra1] : 0;
  assign rd2 = (ra2 != 0) ? regb[ra2] : 0;
  assign rdsave = (rasave != 0) ? regb[rasave] : 0;
endmodule


//Memoria de datos de dos salidas y una entrada
module datafile(input  wire           clk, 
                input  wire           wed,           //se�al de habilitaci�n de escritura
                input  wire [10:0]     fa,           //dir. a leer/escribir
                input  wire [15:0]     sw, 			     //dato a escribir
                output wire [15:0]  d_mem);          //datos leidos

  reg [15:0] mem_data[0:2047]; //memoria de 2048 dir. de 16 bits de ancho

  initial
  begin
    $readmemb("datafile.dat",mem_data); // inicializa los registros a valores conocidos
  end  

  always @(posedge clk)
    if (wed) mem_data[fa] <= sw;	
  
  assign d_mem = mem_data[fa] % 2048;
endmodule

//modulo sumador  
module sum #(parameter WIDTH = 10)
            (input  wire [WIDTH-1:0] a, b, output wire [WIDTH-1:0] y);
  assign y = a + b;
endmodule

//modulo registro para modelar el PC, cambia en cada flanco de subida de reloj o de reset
module registro #(parameter WIDTH = 8)
              (input wire             clk, reset,
               input wire [WIDTH-1:0] d, 
               output reg [WIDTH-1:0] q);

  always @(posedge clk, posedge reset)
    if (reset) q <= 0;
    else       q <= d;

endmodule

//modulo multiplexor, si s=0 sale d0, s=1 sale d1
module mux2 #(parameter WIDTH = 8)
             (input  wire [WIDTH-1:0] d0, d1, 
              input  wire             s, 
              output wire [WIDTH-1:0] y);

  assign y = s ? d1 : d0; 
endmodule

// Módulo multiplexor de 4 entradas
module mux4 #(parameter WIDTH = 8)
             (input  wire [WIDTH-1:0] d0, d1, d2, d3,
              input  wire [1:0]       s,
              output wire [WIDTH-1:0] y);

  reg [WIDTH-1:0] y_temp;

  always @*
    case (s)
      2'b00: y_temp = d0;
      2'b01: y_temp = d1;
      2'b10: y_temp = d2;
      2'b11: y_temp = d3;
    endcase

  assign y = y_temp;
endmodule


//Biestable para el flag de cero
//Biestable tipo D s�ncrono con reset as�ncrono por flanco y entrada de habilitaci�n de carga
module ffd(input wire clk, reset, d, carga, output reg q);

  always @(posedge clk, posedge reset)
    if (reset)
	    q <= 1'b0;
	  else
	    if (carga)
	      q <= d;

endmodule 

//Pila de la CPU de una salida y una entrada
module stackfile(input  wire        clk,
                 input  wire        rws, wsp,     //se�al de habilitaci�n de escritura
                 input  wire [15:0] push_d, 		  //dato a escribir
                 output reg [15:0]  pop_d);       //datos leidos

  reg [15:0] mem_stack[0:1023]; //memoria de 1024 dir. de 16 bits de ancho
  reg [9:0] current_sp;

  initial
  begin
    $readmemb("stackfile.dat",mem_stack); // inicializa los registros a valores conocidos
    current_sp = 10'b0000000000;
  end

  always @(negedge clk)
    begin
      if (!rws) pop_d = ((current_sp == 10'b0000000000) ? 0 : mem_stack[(current_sp - 1) % 1024]);
      if (rws) mem_stack[current_sp] = push_d;
      if (wsp) current_sp = ((!rws) ? ((current_sp == 10'b0000000000) ? 0 : (current_sp - 10'b0000000001)) : current_sp + 1);
    end
endmodule
